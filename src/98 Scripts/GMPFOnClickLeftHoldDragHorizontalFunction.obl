scn GMPFOnClickLeftHoldDragHorizontalFunction

;;@TYPE~User-Defined Function~
;;@PUBL~Private~
;;@DESC~When the user holds the left mouse button and is on a vertical scroll bar,
;;this function will get the height values of scroll bar + the marker and return said values~
;;@PARA~@string::sTile::The menu string that will contain a scroll bar~
string_var sTile

string_var sShortTile 	;@string::sShortTile::The tile without the marker tile

float fMarkerWidth		;@float::fMarkerWidth::Current height of the marker

float fScrollWidth		;@float::fScrollWidth::Width of scroll bar
float fScrollStart		;@float::fScrollStart::Start Y position of scroll bar
float fScrollEnd			;@float::fScrollEnd::End Y position of scroll bar
float fScrollTotal		;@float::fScrollTotal::Total height of scrol bar

array_var aX				;@array::aX::The X values of the ancestry
float fSliderStart		;@float::fSliderStart::The amount of X values to slider's start
int iIndex				;@int::iIndex::Index

Begin Function {sTile}

	;; Slice off scroll marker string from tile
	Let sShortTile := sTile[0:-26]

	;; Only work if touching marker
	if eval (sv_Compare "horizontal_scroll_marker" sTile == -2)
		return
	endif

	;; Get slider start position
	let aX := call GMFGrabParentFloatTraits sShortTile "x"
	while iIndex < ((ar_Size aX) - 1)
		let fSliderStart += aX[iIndex]
		let iIndex += 1
	loop

	;; Enable dragging
	Let GMF.bDragging := 1

	;; Clamp scrolling to bounds of marker and scrollbar
	Let fMarkerWidth := Call GMFGetTileFloatValue (sShortTile + "\horizontal_scroll_marker") "width"
	Let fMarkerWidth *= 2
	Let fScrollWidth := Call GMFGetTileFloatValue sShortTile "width"

	;; Grab total scroll width
	Let fScrollStart := Call GMFGetTileFloatValue sShortTile "user1"
	Let fScrollEnd := Call GMFGetTileFloatValue sShortTile "user2"
	Let fScrollTotal := fScrollEnd - fScrollStart

	;; Export values
	Let GMF.fMarkerWidth := fMarkerWidth
	Let GMF.fSliderStartX := fSliderStart
	Let GMF.fSliderStopX := fSliderStart + fScrollWidth + fMarkerWidth
	Let GMF.fSliderFactor := fScrollTotal / (fScrollWidth - fMarkerWidth)

	sv_Destruct sShortTile

End