scn GMFOnDragInitFunction

;;@TYPE~User-Defined Function~
;;@PUBL~Public~
;;@DESC~This function handles the bulk of dragging, taking an array which contains a stringmap for each
;;index. For each string map, "tile" and "id" are the keys used.
;;@PARA~@array::aTiles::The menu strings that will contain a scroll bar. Each index must be a stringmap
;;that contains the keys "tile" and "id".~
array_var aTiles

int iSize		;@int::iSize::Size of aTiles
int iIndex		;@int::iIndex::Index
string_var sTile	;@string::sTile::Current tile
int iID			;@int::iID::ID of current tile

begin function{aTiles}

	;; INFO:Dragging is a specific mode the menu is running in
	;;      We need to have dragging be the first that can succeed
	let iSize := ar_Size aTiles
	let iIndex := 0

	if eval GMF.bDragging
		while iIndex < iSize
			let sTile := aTiles[iIndex]["tile"]
			let iID := aTiles[iIndex]["id"]
			Call GMPFOnDragMainFunction sTile iID
			let iIndex += 1
		loop
	elseif eval IsKeyPressed3 256 ; LMB
		while iIndex < iSize
			let sTile := aTiles[iIndex]["tile"]
			let iID := aTiles[iIndex]["id"]
			Call GMPFOnHoldLeftMouseMainFunction sTile iID
			let iIndex += 1
		loop
	endif


	sv_Destruct sTile
end